/**
 * @file SimSettings.hoc
 * @brief To help co-ordinating simulations with NEURON and CoreNEURON.
 * @remark Copyright Â© BBP/EPFL 2005-2017; All rights reserved. Do not distribute without further notice.
 */

{load_file("defvar.hoc")}

simulatorMode = 0  // corresponds to NEURON
objref coreneuronDataDir, coreneuronOutputDir


begintemplate SimSettings

public init, interpret, execResult
public getSimulatorMode, generateData, runNeuron, getCoreneuronDataDir, coreNeuronUsed, getCoreneuronOutputDir
public NEURON, CORENEURON, NEURON_CORENEURON

external simulatorMode, coreneuronDataDir, simulator, coreneuronOutputDir
objref this

/* BlueConfig can specify simulator mode : NEURON, CORENEURON or NEURON_CORENEURON
 *
 * NEURON     : Run simulation using NEURON (current default behaviour)
 * CORENEURON : Build model with NEURON, dump dataset to disk and continue simulation with CoreNEURON
 * NEURON_CORENEURON : Build model with NEURON, dump dataset to disk, run simulation with NEURON
 *       and also run same simulation with CoreNEURON. This is more for debugging purpose and
 *       comparing results between two simulators.
 */

// constants for simulation mode handling
proc init() {
    NEURON = 0
    CORENEURON = 1
    NEURON_CORENEURON = 2
}

/**
 * Given the Run block from a BlueConfig, check simulator to be used
 * $o1 ParsedRun from a BlueConfig (type is Map)
 */
proc interpret() { localobj runInfo, pc, rng, commandString
    runInfo = $o1
    pc = new ParallelContext()

    if( runInfo.exists( "Simulator" ) ) {
        if( pc.id == 0 ) print "Taking Simulator mode from BlueConfig"
        simulator = runInfo.get( "Simulator" ).s
    }

    // check if simulator option is set in BlueConfig, abort if invalid
    if( strcmp( simulator, "NEURON" ) == 0 ) {
        simulatorMode = NEURON
    } else if( strcmp( simulator, "CORENEURON" ) == 0 ) {
        simulatorMode = CORENEURON
    } else if( strcmp( simulator, "NEURON_CORENEURON" ) == 0 ) {
        simulatorMode = NEURON_CORENEURON
    } else {
        if( pc.id() == 0 ) {
            print "Invalid SimulatorMode '", simulator, "'; use NEURON, CORENEURON or NEURON_CORENEURON. Terminating"
        }
        pc.barrier()
        quit()
    }

    // create directory for dumping dataset into separate directory inside OutputRoot
    if( simulatorMode != NEURON ) {
        // create directory for dumping coreneuron binary dataset
        coreneuronDataDir = new String()
        coreneuronOutputDir = new String()
        sprint(coreneuronDataDir.s, "%s/coreneuron_input", runInfo.get( "OutputRoot" ).s )
        sprint(coreneuronOutputDir.s, "%s/", runInfo.get( "OutputRoot" ).s )
        if( pc.id() == 0 ) {
            commandString = new String()
            execResult = 0
            sprint( commandString.s, "%s.execResult = checkDirectory( \"%s\" )", this, coreneuronOutputDir.s )
            execute( commandString.s )
            if( execResult < 0 ) {
                execerror( "Error while creating directory '", coreneuronOutputDir.s, "'.  Terminating." )
            }

            // if we are Restoring, then we will reuse the datadirectory
            if( runInfo.exists( "Restore" ) ) {
                nrnpython( "import os" )
                sprint( commandString.s, "os.symlink( os.path.realpath(\"%s/../coreneuron_input\"), \"%s\" )", runInfo.get( "Restore" ).s, coreneuronDataDir.s )
                print commandString.s
                nrnpython( commandString.s )
            } else {
              sprint( commandString.s, "%s.execResult = checkDirectory( \"%s\" )", this, coreneuronDataDir.s )
              execute( commandString.s )
              if( execResult < 0 ) {
                execerror( "Error while creating directory '", coreneuronDataDir.s, "'.  Terminating." )
              }
            }
        }
    }

    if( pc.id() == 0 ) {
        print "SimulatorMode set to '", simulator, "'"
    }

    pc.barrier()
}

func getSimulatorMode() {
    return simulatorMode
}

// NEURON simulator mode doesn't need data generation
func generateData() {
    return (simulatorMode != NEURON)
}

// true if coreneuron is specified
func coreNeuronUsed() {
    return (simulatorMode != NEURON)
}

// Don't run NEURON if "exclusive" CoreNEURON mode specified
func runNeuron() {
    return (simulatorMode != CORENEURON)
}

// Directory where data will be dumped for CoreNEURON
obfunc getCoreneuronDataDir() {
    return coreneuronDataDir
}

// Directory where coreneuron will be output files
obfunc getCoreneuronOutputDir() {
    return coreneuronOutputDir
}

endtemplate SimSettings


// Instantiate singleton  -  load_file() makes sure runs only once
objref simConfig
simConfig = new SimSettings()
